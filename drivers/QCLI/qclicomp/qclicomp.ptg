/* qclicomp.ptg */
File:
  "Source File\n"
  "  8 Word Header\n"
  "  Index: \n"
  $1 /* Waveform Index */
  "\n"
  "  Total Length: " $3 long "\n\n"
  "Waveform Summaries:\n\n"
  $2 /* Waveform Summaries */

ICOSWave: "ICOS Waveform " $1 " @ " $17 long " {" "\n"
  "  Sample Rate:      " $3 $2 "\n"
  "  Net Ramp Samples: " $5 $4 "\n"
  $6 /* Iz */
  $7 /* dI/dt */
  $8 /* Tcycle */
  $12 /* Fcycle */
  $9 /* Stepsize */
  "  Scycle:      " $15 int " " $16 int "\n"
  "  Tz:          " $13 $11 "\n" /* Tz Actual and specified */
  $14 /* Ton, Toff, Trcy+Trcypre */
  $10 /* Ramps */
  "}\n"

Ramp:
  "  Ramp {\n"
  "    Istart:    " $2 $1 "\n"
  "    Istop:     " $4 $3 "\n"
  "    Tramp:     " $6 $5 "\n"
  "    Sramp:     " $7 int "\n"
  "  }\n"

WaveIndex: "    " $1 ": " $2 long "\n"

Iz:         "  Iz:          " $1 "\n"
dIdt:       "  dI/dt:       " $1 double " A/sec\n"
SampleRate: $1 double "/Sec/" $2 int
Specd:      " (" $1 ")"
Long:       $1 long
Tcycle:     "  Tcycle:      " $1 double " msec\n"
Fcycle:     "  Fcycle:      " $1 double " Hz\n"
Stepsize:   "  Stepsize:    " $1 long " usec\n"
Current:    $1 double " mA"
Time:       $1 double " msec"
TzSample:   $1 int " " $2 string "Samples"
Percent:    $1 double "%"
Tz:         $1 ", " $2 long "/" $3 long
              " Samples, " $4 double "%"
Waves:   $1 { "\n" } $2
Tonoff:
  "  Ton/Toff:    " $1 long "/" $2 long " usec\n"
  "  Trecycle:    " $3 long " usec\n"
