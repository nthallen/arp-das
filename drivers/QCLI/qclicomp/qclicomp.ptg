/* qclicomp.ptg */
File:
  "\nCompiled Output:\n"
  $1 /* Header */
  ": Index: \n"
  $2 /* Waveform Index */
  $3 " Fill at end of index\n"
  $4 /* Waveforms */
  ": Total Length: " $5 long "\n"

Header:
  $1 " Program Length\n"
  [ ptg_output_time ]
  $2 " Padding\n"

Hex:
  [ PTG_OUTPUT_HEX $1 short $2 int ]

FileSum: /* Summary of waveforms */
  "Waveform Summaries:\n\n"
  $1 /* Waveform Summaries */

ICOSHdr:
  $3 " ICOS Waveform " $1 int " " $2 "\n"
  $9 /* DAC initialization */
  $4 " Ton-1\n"
  $5 " Toff-1\n"
  $6 " Tpre (0)\n"
  $7 " Loop Address\n"
  $8 " Loop Length\n"

DACs:
  [ PTG_OUTPUT_1HEX $3 short ] " DAC0 - " $4 double " Amps\n"
  [ PTG_OUTPUT_1HEX $1 short ] " DAC1 - " $2 double " Amps/Sec\n"
  [ PTG_OUTPUT_1HEX $5 short ] " DAC2 - " $6 double " Amps\n"
  [ PTG_OUTPUT_1HEX $7 short ] " DAC3 - " $8 double " Amps\n"

ICOSWave: "ICOS Waveform " $1 " @ " $17 long " {" "\n"
  "  Sample Rate:      " $3 $2 "\n"
  "  Net Ramp Samples: " $5 $4 "\n"
  $6 /* Iz */
  $7 /* dI/dt */
  $8 /* Tcycle */
  $12 /* Fcycle */
  $9 /* Stepsize */
  "  Scycle:      " $15 int " " $16 int "\n"
  "  Tz:          " $13 $11 "\n" /* Tz Actual and specified */
  $14 /* Ton, Toff, Trcy+Trcypre */
  $10 /* Ramps */
  "}\n"

ICOSProg:
  $1 /* Tz */
  $2 /* Ramps */
  $3 /* Tz' */
  $4 /* Trcy */
  
Ramp:
  "  Ramp {\n"
  "    Istart:    " $2 $1 "\n"
  "    Istop:     " $4 $3 "\n"
  "    Tramp:     " $6 $5 "\n"
  "    Sramp:     " $7 int "\n"
  "  }\n"

WaveIndex: $2 " " $1 "\n"

Iz:         "  Iz:          " $1 "\n"
dIdt:       "  dI/dt:       " $1 double " A/sec\n"
SampleRate: $1 double "/Sec/" $2 int
Specd:      " (" $1 ")"
Long:       $1 long
Tcycle:     "  Tcycle:      " $1 double " msec\n"
Fcycle:     "  Fcycle:      " $1 double " Hz\n"
Stepsize:   "  Stepsize:    " $1 long " usec\n"
Current:    $1 double " mA"
Time:       $1 double " msec"
TzSample:   $1 int " " $2 string "Samples"
Percent:    $1 double "%"
Tz:         $1 ", " $2 long "/" $3 long
              " Samples, " $4 double "%"
Waves:   $1 { "\n" } $2
Tonoff:
  "  Ton/Toff:    " $1 long "/" $2 long " usec\n"
  "  Trecycle:    " $3 long " usec\n"

Fill: [ PTG_OUTPUT_HEX $1 short $2 int ]
ICOSFill: [ PTG_OUTPUT_ICOSHEX $1 short $2 int ] "\n"

RingHdr:
  $3 " Ringdown Waveform " $1 int " " $2 "\n"
  $9 /* DAC initialization */
  $4 " Ton-1\n"
  $5 " Toff-1\n"
  $6 " Tpre-1\n"
  $7 " Loop Address\n"
  $8 " Loop Length\n"

RingData:
  [ PTG_OUTPUT_1HEX $1 short ] " " $2 double " Amps\n"
