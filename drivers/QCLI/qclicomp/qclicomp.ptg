/* qclicomp.ptg */
File:
  "\nCompiled Output:\n"
  "  8 Word Header\n"
  "  Index: \n"
  $1 /* Waveform Index */
  $2 /* Fill at end of index */
  $3 /* Waveforms */
  "  Total Length: " $4 long "\n"

FileSum: /* Summary of waveforms */
  "Waveform Summaries:\n\n"
  $1 /* Waveform Summaries */

ICOSHdr:
  "\n"
  "  ICOS Waveform " $1 int " " $6 "\n"
  $7 /* DAC initialization */
  "  " $2 long ": Ton-1\n"
  "  " $3 long ": Toff-1\n"
  "  0: Tpre\n"
  "  " $4 long ": Loop Address\n"
  "  " $5 int ": Loop Length\n"

DACs:
  "  " $1 double " Amps/Sec DAC1\n"
  "  " $2 double " Amps DAC2\n"
  "  " $3 double " Amps DAC3\n"
  "  " $4 double " Amps DAC4\n"

ICOSWave: "ICOS Waveform " $1 " @ " $17 long " {" "\n"
  "  Sample Rate:      " $3 $2 "\n"
  "  Net Ramp Samples: " $5 $4 "\n"
  $6 /* Iz */
  $7 /* dI/dt */
  $8 /* Tcycle */
  $12 /* Fcycle */
  $9 /* Stepsize */
  "  Scycle:      " $15 int " " $16 int "\n"
  "  Tz:          " $13 $11 "\n" /* Tz Actual and specified */
  $14 /* Ton, Toff, Trcy+Trcypre */
  $10 /* Ramps */
  "}\n"

ICOSProg:
  $1 /* Tz */
  $2 /* Ramps */
  $3 /* Tz' */
  $4 /* Trcy */
  
Ramp:
  "  Ramp {\n"
  "    Istart:    " $2 $1 "\n"
  "    Istop:     " $4 $3 "\n"
  "    Tramp:     " $6 $5 "\n"
  "    Sramp:     " $7 int "\n"
  "  }\n"

WaveIndex: "  " $2 long ": " $1 "\n"

Iz:         "  Iz:          " $1 "\n"
dIdt:       "  dI/dt:       " $1 double " A/sec\n"
SampleRate: $1 double "/Sec/" $2 int
Specd:      " (" $1 ")"
Long:       $1 long
Tcycle:     "  Tcycle:      " $1 double " msec\n"
Fcycle:     "  Fcycle:      " $1 double " Hz\n"
Stepsize:   "  Stepsize:    " $1 long " usec\n"
Current:    $1 double " mA"
Time:       $1 double " msec"
TzSample:   $1 int " " $2 string "Samples"
Percent:    $1 double "%"
Tz:         $1 ", " $2 long "/" $3 long
              " Samples, " $4 double "%"
Waves:   $1 { "\n" } $2
Tonoff:
  "  Ton/Toff:    " $1 long "/" $2 long " usec\n"
  "  Trecycle:    " $3 long " usec\n"

Fill:  "  " $2 string " repeated " $1 int " times\n"
ICOSFill:
  "  " $2 string " " $3 string " " $4 string
  " repeated " $1 int " times\n"
