SourceFile : Wave+ .
Wave : ICOSWave / RingWave .
ICOSWave : 'ICOS' 'Waveform' Ident '{' ICOSCmds '}' .
ICOSCmds : ( ICOSCmd ';' )+ .
ICOSCmd : TzDef / IzDef / NcoaddDef / ICOSNsampleDef / ICOSRateDef / dIdt / ICOSRamp .
TzDef : 'Tz' '=' TzSpec .
TzSpec : Time / SampleSpec / Percent .
SampleSpec : Integer 'Raw' 'Samples' / Integer 'Samples' .
Percent : FloatVal '%' .
IzDef : 'Iz' '=' Current .
NcoaddDef : 'Ncoadd' '=' Integer .
ICOSNsampleDef : NsampleDef .
ICOSRateDef : RateDef .
NsampleDef : 'Nsample' '=' Integer .
RateDef : 'SampleRate' '=' Integer RateUnit Naverage .
Naverage : / '/' Integer .
dIdt : 'dI' '/' 'dt' '=' Current '/' TimeUnit .

ICOSRamp :
  'Ramp' Istart Istop PrepTime /
  'Ramp' Istart Istop /
  'Ramp' Istart PrepTime /
  'Ramp' Istop PrepTime .

Istart : 'from' Current .
Istop : 'to' Current .
PrepTime : ( 'in' / 'for' ) Time .

Current : FloatVal CurrentUnit .
Time : FloatVal TimeUnit .

FloatVal : Integer / Float .
CurrentUnit : 'A' / 'mA' / 'uA' .
TimeUnit : 'sec' / 'msec' / 'usec' .
RateUnit : 'Hz' / 'kHz' / 'MHz' / '/' TimeUnit .

RingWave : 'Ringdown' 'Waveform' Ident '{' RingCmds '}' .
RingCmds : ( RingCmd ';' )+ .
RingCmd : TonDef / ToffDef / TpreDef / IzDef / RingNsampleDef / RingRateDef / RingData .
RingNsampleDef : NsampleDef .
RingRateDef : RateDef .
TonDef : 'Ton' '=' TonVal .
TonVal : Time / FloatVal '%' .
ToffDef : 'Toff' '=' Time .
TpreDef : 'Tpre' '=' TpreVal .
TpreVal : Time / Integer 'Samples' .
RingData : 'I' '=' ( RingCurrent / CurrentSteps ) .
RingCurrent : Current .
CurrentSteps : Current 'to' Current 'in' RingStep 'Steps' .
RingStep : RingCount / RingStepsize .
RingCount : Integer .
RingStepsize : Current .
